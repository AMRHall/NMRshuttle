;2D pseudo-2D M2SvdfS2M 
;avance-version
;M2D-vd list-T00 filter-S2M (use to measure TS)
;Including trigger I/O for NMR shuttle

;$OWNER=amrh
#include <Avance.incl>
#include <Grad.incl>

define delay d1p180
"d1p180 = d1 + p1*2"

3m
1 ze

define delay dmot
"dmot = vd - d10*2 + (cnst11 + cnst12 + cnst20 + cnst30 + cnst31)*0"

2 d7

10u pl1:f1
(p1 ph30):f1
	
;M2S
; first echo train 
3 d1
	 (p1 ph0):f1	
	 (p1*2 ph1):f1
	 (p1 ph0):f1
 d1 ipp0 ipp1
 lo to 3 times l2
 
 (p1 ph2):f1
 d1p180

; second echo train
4 d1 
 	 (p1 ph0):f1
	 (p1*2 ph1):f1
	 (p1 ph0):f1
  d1 ipp0 ipp1
  lo to 4 times l1

; move sample to low field strength
1u setnmr3|8
d10
1u setnmr3^8

; execute variable delay
dmot

; move sample back to high field
1u setnmr3|9
d10
1u setnmr3^9

;selection
40u UNBLKGRAD
p16:gp1
2m 
(p1 ph3):f1
p19:gp2
2m 
(p1 ph3):f1
(p1 ph4):f1
p29:gp3
40u BLKGRAD
10m

;S2M
; second echo train 
5 d1
	 (p1 ph0):f1	
	 (p1*2 ph1):f1
	 (p1 ph0):f1
  d1 ipp0 ipp1
  lo to 5 times l1
 
  d1p180
  (p1 ph2):f1

; first echo train
6 d1 
	 (p1 ph0):f1
	 (p1*2 ph1):f1
	 (p1 ph0):f1
  d1 ipp0 ipp1
  lo to 6 times l2
	
go=2 ph31
30m wr #0 if #0 zd ivd
lo to 1 times td1
exit

ph0 = 0 0 2 2 0 0 2 2 2 2 0 0 2 2 0 0 
ph1 = 1 1 3 3 1 1 3 3 3 3 1 1 3 3 1 1
ph2 = 0 0

ph3 = (65536) 9965 9965 
ph4 = 2 2
ph30 = 1 3
ph31 = 1 3

;l2 : n1 is 2*n2
;l1 : n2
;p1 : pw90 at high power
;p7 : pw90 at low power
;pl1 : power level high
;pl2 : power level low

;d1: tau_J
;d7 : pre-delay
;d10 : sample motion time

;vd : tau ZF (singlet relaxation time)

;cnst11 : Operation mode (1 = constant velocity, 2 = velocity sweep)
;cnst12 : Tube type (1 = standard 5mm glass tube, 2 =5 mm high pressure tube, 3 = 10mm high pressure tube)
;cnst20 : Low field strength (mT)
;cnst30 : Target motor speed (optional) (Range: 2...2048. If this value is not set the motor will use the default speed set in NMRShuttleSetup.py).
;cnst31 : Target motor acceleration (optional) (Range: 2...2048. If this value is not set the motor will use the default speed set in NMRShuttleSetup.py).

; other parameters

;define VDLIST


