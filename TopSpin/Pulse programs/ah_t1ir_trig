;t1ir_trig
;avance-version (12/01/11)
;T1 measurement using inversion recovery
;Trigger for NMR low field shuttle
;$CLASS=HighRes
;$DIM=2D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


#include <Avance.incl>


"p2=p1*2"
"d12=30m"


"acqt0=-p1*2/3.1416"

1 ze

define delay dmot
"dmot = vd - (d10*2 - d11 + cnst11 + cnst12 + cnst20 + cnst30 + cnst31)*0"

2 d1
  p2 ph1

	; move sample to low field strength
	1u setnmr3|8
	10m trigne2
	1u setnmr3^8

	; execute variable delay
	dmot

	; move sample back to high field
	1u setnmr3|9
	10m trigne2
	1u setnmr3^9

	; extra delay for positioning
	d11

  p1 ph2
  go=2 ph31
  d12 wr #0 if #0 ivd
  lo to 1 times td1
exit


ph1=0 2 
ph2=0 0 2 2 1 1 3 3
ph31=0 0 2 2 1 1 3 3


;pl1 : f1 channel - power level for pulse (default)
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;d1 : relaxation delay; 1-5 * T1
;d12: delay for disk I/O                             [30 msec]
;vd : variable delay, taken from vd-list
;ns: 8 * n
;ds: 4
;td1: number of experiments = number of delays in vd-list
;FnMODE: undefined


; PARAMETERS USED BY SHUTTLE MOTOR:

;d10 : sample motion time
;d11 : positioning delay
;cnst11 : Operation mode (1 = constant velocity, 2 = velocity sweep, 3 = constant time)
;cnst12 : Stall guard settings (1 = standard glass tube, 2 = high pressure tube, 3 = temperature control system)
;cnst20 : Low field strength (mT)
;cnst30 : Target motor speed (optional) (Range: 2...2048. If this value is not set the motor will use the default speed).
;cnst31 : Target motor acceleration (optional) (Range: 2...2048. If this value is not set the motor will use the default speed).



;define VDLIST

;this pulse program produces a ser-file (PARMOD = 2D)



;$Id: t1ir,v 1.13 2012/01/31 17:49:28 ber Exp $